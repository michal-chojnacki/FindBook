apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.github.michalchojnacki.findbook"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "API_KEY", "\"" + getApiKey() + "\""
        }
        release {
            buildConfigField "String", "API_KEY", "\"" + getApiKey() + "\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    flavorDimensions "api"

    productFlavors {
        apiMock {
            dimension "api"
            applicationIdSuffix ".mock"
            versionNameSuffix " - mock"
        }

        apiProduction {
            dimension "api"
        }
    }

}

static def getApiKey() {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['API_KEY']
}

apply from: './dependencies.gradle'
